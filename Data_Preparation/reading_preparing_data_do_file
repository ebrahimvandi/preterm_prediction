cd "your local directory" 
///////////////////////////////////////////////////////////////////////////////////////////////////////////
								//Reading CDC data
								
//first, read 2016 data with dictionary of 2015
drop _all
//infile using linkpe2015usnmat.dct.txt, using("\Downloads\VS15LINK.DUSDENOM.AllCnty.txt")
infile using linkpe2015usnmat.dct.txt, using("\Downloads\VS16LINK.DENOMUS.AllCnty.txt")
 

/////////////////////////////////////////////////////////////////////////////////////////////////////////// 
// Validation
keep if restatus!=4
 by mrstatefips, sort: count  //This gives birth per state
 by mrstatefips, sort: count if oegest_r3==1  //checking preterms by state
 by mrstatefips, sort: count if oegest_r3==1 & me_rout==1  //checking spontaneous preterms by state
  gen cntr=1
  by mrstatefips mrcntyfips, sort: count
 //check with: https://wonder.cdc.gov/controller/datarequest/D66;jsessionid=B3B0E3648B9EB00781956CF4C4CBAD68

/////////////////////////////////////////////////////////////////////////////////////////////////////////// 
 
 								//Generating individual-level variables from CDC data

// Data Preperation for 2016 database
keep if restatus!=4
drop if dplural!=1  //drop multiples
drop if oegest_r3==3  //drop unknown preterm birth status


gen preterm=0
replace preterm=1 if oegest_r3==1 // combgest<37 ////& me_rout==1  //Generating preterm birth based on obstetric estimate
order preterm, first

gen STD=1 if (ip_gon=="Y" | ip_syph=="Y" | ip_chlam=="Y")  //generate sexually transmitted disease for those who has any of these diseases
replace STD=0 if (ip_gon=="N" & ip_syph=="N" & ip_chlam=="N")
replace STD=. if missing(STD)
drop ip_gon ip_syph ip_chlam


gen Adequate=1 if previs!=99 & ///
((oegest_comb<=13 & previs>=1 ) | ///
(oegest_comb>=14 & oegest_comb<=17 & previs>=2 ) | ///
(oegest_comb>=18 & oegest_comb<=21 & previs>=3 ) | ///
(oegest_comb>=22 & oegest_comb<=25 & previs>=4 ) | ///
(oegest_comb>=26 & oegest_comb<=29 & previs>=5 ) | ///
(oegest_comb>=30 & oegest_comb<=31 & previs>=6 ) | ///
(oegest_comb>=32 & oegest_comb<=33 & previs>=7 ) | ///
(oegest_comb>=34 & oegest_comb<=35 & previs>=8 ) | ///
(oegest_comb>=36 & previs>=9 )) & ///
(precare<=3 & !missing(precare))

gen Inadequate=1 if previs==99 | ///
((oegest_comb>=14 & oegest_comb<=21 & previs==0) | ///
(oegest_comb>=22 & oegest_comb<=29 & previs<=1) | ///
(oegest_comb>=30 & oegest_comb<=31 & previs<=2) | ///
(oegest_comb>=32 & oegest_comb<=33 & previs<=3) | ///
(oegest_comb>=34 & previs<=4)) | ///
(precare>=7 & !missing(precare))

gen Intermediate=1 if missing(Adequate) & missing(Inadequate)
replace Adequate=0 if missing(Adequate)
replace Inadequate=0 if missing(Inadequate)
replace Intermediate=0 if missing(Intermediate)


keep preterm combgest mager mrstatefips mrcntyfips mbrace mracehisp dmar meduc Adequate Inadequate Intermediate ///
fagecomb fbrace feduc priorlive priordead precare5 wic cig_0 cig_1 cig_2 cig_3 dplural ///
STD sex wic rf_diab rf_gdiab rf_eclam rf_phyper rf_ghyper rf_ppterm rf_inftr rf_cesar rf_cesarn pay bmi illb_r11

replace mrstatefips="NY" if mrstatefips=="YC"
gen StCnty = mrstatefips+ mrcntyfips
//tabulate mrstatefips, generate(mrstatefips)
//tabulate mbrace, generate(mbrace)
//rename mbrace4 Asian
generate race= "White" if mbrace==1
replace race="Hispanic" if  mracehisp==7
replace race= "Black" if mbrace==2
replace race= "Indian" if mbrace==3
replace race= "Asian" if mbrace==4
replace race= "Unk" if mbrace==9  
//replace race= "Unk" if mraceimp=="1" // changing imputed race 



//Change father's race
generate Race_F= "White" if fbrace==1
replace Race_F="Hispanic" if  fbrace==7
replace Race_F= "Black" if fbrace==2
replace Race_F= "Indian" if fbrace==3
replace Race_F= "Asian" if fbrace==4
replace Race_F= "Unk" if fbrace==9  
//replace race= "Unk" if mraceimp=="1" // changing imputed race 



replace priorlive=. if priorlive==99 | priorlive>16 //because only one person had more than 16(21) children
replace priordead=. if priordead==99 | priordead>12 //because only three person had more than 12(13, 29) children

rename dmar Married //2 is unmarried in dmar
replace Married=0 if Married==2
replace fagecomb=. if fagecomb==99 //replace unknown father's age with missing value


//drop if fagecomb==99 //father age of 99 is unknown *Note: This will drop about half a million data point
//gen Fage2= fagecomb*fagecomb //father age squared

//tabulate fbrace, generate(fbrace)
//tabulate feduc, generate(feduc) //feduc of 9 is unknown
//tabulate precare5, generate(precare5) //precare5 of "U" is unknown

//tabulate cig_rec, generate(cig_rec) //cig_rec of "U" is unknown
replace cig_0=. if cig_0==99 //unknown cig use
rename cig_0 cig_before
replace cig_1=. if cig_1==99
replace cig_2=. if cig_2==99
replace cig_3=. if cig_3==99

gen WIC=1 if (wic=="Y")
replace WIC=0 if wic=="N"
//drop if missing(WIC)

gen sex_M=1 if (sex=="M")
replace sex_M=0 if sex=="F"
gen diab=1 if (rf_diab=="Y" | rf_gdiab=="Y")
replace diab=0 if (rf_diab=="N" & rf_gdiab=="N")
//drop if missing(diab)

gen hyper=1 if (rf_phyper=="Y" | rf_ghyper=="Y" | rf_eclam=="Y")
replace hyper=0 if (rf_phyper=="N" & rf_ghyper=="N" & rf_eclam=="N")
//drop if missing(hyper)

// Drop missing values
foreach v of var rf_ppterm rf_inftr rf_cesar{ 
	drop if `v'=="U" 
	drop if missing(`v')
	gen `v'_r=1 if  `v'=="Y"
	replace `v'_r=0 if `v'=="N"
}


replace pay="8" if pay=="4" | pay=="5" | pay=="6"
//tabulate pay, generate(pay) //pay of 9 is unknown
replace pay="Medicaid" if pay=="1"
replace pay="Private" if pay=="2"
replace pay="Self" if pay=="3"
replace pay="Other" if pay=="4" | pay=="8"
replace pay="Unk" if pay=="9"

replace bmi=. if bmi==99.9 //unknown weights
//drop if combgest==99 //unknonwn gestational age

replace illb_r11=. if illb_r11==99 //88 is not applicable (first child) and 99 is unknown and 00 is plural which we have it already
//drop if interval==1 //This is related to 00, which is plural that we considered it already
generate interval= "Plural" if illb_r11==0
	replace interval = "<11" if illb_r11==1
	replace interval= "12_17" if illb_r11==2
	replace interval= "18_23" if illb_r11==3
	replace interval= "24_35" if illb_r11==4
	replace interval= "36_47" if illb_r11==5
	replace interval= "48_59" if illb_r11==6
	replace interval= "60_71" if illb_r11==7
	replace interval= ">72" if illb_r11==8
	replace interval= "1st child" if illb_r11==88
drop if interval=="Plural"

rename mager Age_M
rename fagecomb Age_F
rename race Race_M
rename meduc Education_M
rename feduc Education_F
rename rf_ppterm_r Previous_preterm
rename rf_inftr_r Infertility_treatment
rename rf_cesar_r Previous_cesareans
	

//keep the main factor variables
keep preterm combgest Age_M Age_F Married bmi priorlive priordead mrstatefips mrcntyfips ///
Race_M Race_F Education_M Education_F STD hyper diab Adequate Inadequate Intermediate ///
cig_1 cig_2 cig_before pay dplural sex_M  StCnty interval WIC Previous_preterm Infertility_treatment Previous_cesareans 

save Linked16.dta, replace


///////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
 
///////////////////////////////////////////////////////////////////////////////////////////////////////////
                         //Cleaning county level datasets
cd "\Data_Preparation"
//import delimited 2016CHR_CSV_Analytic_Data_v2.csv, clear

import excel 2016CHR_CSV_Analytic_Data_v3.xlsx, sheet("2016CHR_CSV_Analytic_Data_v2") firstrow clear


//foreach var of varlist _all {
//  misstable sum `var'   //report sum of missing values per column
//  if r(K_uniq) < 10 drop `var' //drop if number of unique obs in colum is less than 4
// }

 keep STATECODE COUNTYCODE State County PoororfairhealthValue PoorphysicalhealthdaysValue ///
 PoormentalhealthdaysValue AdultsmokingValue AdultobesityValue FoodenvironmentindexValue ///
 PhysicalinactivityValue ExcessivedrinkingValue Alcoholimpaireddrivingdeaths Sexuallytransmittedinfections ///
 TeenbirthsValue UninsuredValue PrimarycarephysiciansValue DentistsValue MentalhealthprovidersValue ///
 PreventablehospitalstaysValue Accesstoexerciseopportunities Limitedaccesstohealthyfoods ///
 HighschoolgraduationValue SomecollegeValue UnemploymentValue ChildreninpovertyValue IncomeinequalityValue ///
 Childreninsingleparenthouseh SocialassociationsValue ViolentcrimeValue InjurydeathsValue DrinkingwaterviolationsValue ///
 Airpollutionparticulatematt SeverehousingproblemsValue DrivingalonetoworkValue LongcommutedrivingaloneVal ///
 ChildmortalityValue InfantmortalityValue DiabetesValue HIVprevalencerateValue ///
 FoodinsecurityValue DrugOverdoseDeathsValue MotorvehiclecrashdeathsValue InsufficientsleepValue ///
 UninsuredadultsValue UninsuredchildrenValue HealthcarecostsValue OtherprimarycareprovidersVal ///
 MedianhouseholdincomeValue Childreneligibleforfreelunch ResidentialsegregationBlack ResidentialsegregationnonWhit ///
 HomiciderateValue PopulationestimateValue Percentofpopulationbelow18y Percentofpopulationaged65ye Percentofpopulationthatisno ///
 PercentofpopulationthatisHi Populationlivinginaruralare PopulationestimateValue

 rename STATECODE state
 gen str3 CountyCode = string( COUNTYCODE ,"%03.0f")
 order CountyCode, before(state)
 gen StCnty = State+CountyCode
 order StCnty, first
 drop state
 rename State state
 
 drop if CountyCode=="000"

 gen Dentists_Ratio= DentistsValue/ PopulationestimateValue*10000
  format %7.4g Dentists_Ratio
 gen Mental_health_providersRatio = MentalhealthprovidersValue/ PopulationestimateValue*10000
 gen Other_PC_Physicians_Ratio = OtherprimarycareprovidersVal / PopulationestimateValue*10000
  format %7.4g Other_PC_Physicians_Ratio
 


 drop COUNTYCODE DentistsValue MentalhealthprovidersValue OtherprimarycareprovidersVal 
 
 
foreach v of varlist _all {		//drop any variables with less than 3050 nonmissing
     count if !missing(`v')
     if `r(N)' <= 3050 {  //Number of unique values of StCnty is  3133
          drop `v'
     }
}
 

 rename PoororfairhealthValue Poor_Health
 rename PoorphysicalhealthdaysValue Poor_Physical_Days
 rename PoormentalhealthdaysValue Poor_Mental_Days
 rename AdultsmokingValue Adult_Smoking
 rename AdultobesityValue Adult_Obesity
 rename FoodenvironmentindexValue Food_Envir
 rename PhysicalinactivityValue Physical_Inacitvity
 rename Accesstoexerciseopportunities Access_to_Exercise
 rename ExcessivedrinkingValue Exessive_Drinking
 rename Alcoholimpaireddrivingdeaths Alchohol_Driving_Death
 rename PreventablehospitalstaysValue Prev_Hospital_Stays
 rename SomecollegeValue College_Degree
 rename UnemploymentValue Unemployment
 rename ChildreninpovertyValue Children_In_Pov
 rename IncomeinequalityValue Income_Inequality
 rename Childreninsingleparenthouseh Chil_in_Single_Parent
 rename SocialassociationsValue Social_Asoc
 rename Airpollutionparticulatematt Air_Particulate_Matter
 rename DrinkingwaterviolationsValue Drinking_Water_Viol
 rename SeverehousingproblemsValue Severe_Housing_Prob
 rename DrivingalonetoworkValue Driving_Alone_Work
 rename LongcommutedrivingaloneVal Long_Drive_Work
 rename DiabetesValue Diabetes
 rename FoodinsecurityValue Food_Insecurity
 rename Limitedaccesstohealthyfoods Lim_Healthy_Food
 rename InsufficientsleepValue Insuf_Sleep
 rename UninsuredValue Uninsured_Total
 rename UninsuredadultsValue Uninsured_Adults
 rename UninsuredchildrenValue Uninsured_Children
 rename Percentofpopulationbelow18y Perc_Und_Eighteen
 rename Percentofpopulationaged65ye Perc_Ove_SixtyFive
 rename Percentofpopulationthatisno Black_Perc
 rename PercentofpopulationthatisHi Hisp_Perc
 rename Populationlivinginaruralare Rural_Perc
 rename HealthcarecostsValue Healthcare_Cost
 rename MedianhouseholdincomeValue Median_Household_Income
  
  
foreach var of varlist Poor_Health Adult_Obesity Physical_Inacitvity Access_to_Exercise ///
Exessive_Drinking Alchohol_Driving_Death Uninsured_Total College_Degree ///
Adult_Smoking Unemployment Children_In_Pov Chil_in_Single_Parent Severe_Housing_Prob ///
Driving_Alone_Work Long_Drive_Work Diabetes Food_Insecurity Lim_Healthy_Food Insuf_Sleep ///
Uninsured_Adults Uninsured_Children Perc_Und_Eighteen Perc_Ove_SixtyFive Black_Perc Hisp_Perc Rural_Perc {
 
 replace `var' = `var' *100
 recast byte `var', force
}

foreach var of varlist Prev_Hospital_Stays Social_Asoc Air_Particulate_Matter Dentists_Ratio Other_PC_Physicians_Ratio {
 recast byte `var', force
}



foreach var of varlist Poor_Physical_Days Poor_Mental_Days Food_Envir Income_Inequality {
 replace `var'= round(`var',0.1)
 recast float `var', force
 format %6.1g `var'
}


 drop Food_Envir //because it is an average of Food_Insecurity and Lim_Healthy_Food   

 recast str4 Drinking_Water_Viol
 format %4s Drinking_Water_Viol
 
 save cnty_data_CHR.dta, replace

///////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
 
///////////////////////////////////////////////////////////////////////////////////////////////////////////
                         //Preparing Area Health Resource Files
cd "\Data_Preparation"
import excel AHRF.xlsx, sheet("Sheet1") firstrow clear


 tostring FIPS, generate(FIPS_R)
 gen str3 CountyCode= substr(FIPS_R, -3,3)
 gen StCnty = StateAbbrev+CountyCode
 drop FIPS FIPS_R StateAbbrev CountyCode
 order StCnty, first

 collapse (mean)  TotalNumberHospitals STGFTETotalFacilPrsnl TotalHospitalBeds TotalNursingHomeLicensedBeds ///
 STGHospwObstetricCare PopulationDensityperSquareMi ///
 PersonsinPoverty Females65withoutHealthI ///
 MedianHomeValue FemalesDivorced MDsNonFedandFedTotalAct ///
 MDsNFObGynGeneralTotal TotHospsInpatientDays TotHospsOutpatientVisits PopulationEstimate, by(StCnty)

 rename PopulationEstimate Cnty_Population
 gen Number_of_Hospitals_Per_10k = TotalNumberHospitals/Cnty_Population*10000 
 gen Hospital_Total_Prsnl_Per_10k = STGFTETotalFacilPrsnl/Cnty_Population*10000 
 gen Hospital_Beds_Per_10k = TotalHospitalBeds/Cnty_Population*10000  
 gen Nursing_Home_Beds_Per_10k = TotalNursingHomeLicensedBeds/Cnty_Population*10000  
 gen Hosp_w_Obstetric_Care_Per_10k = STGHospwObstetricCare/Cnty_Population*10000  
 gen Population_Density=round(PopulationDensityperSquareMi, 1)
 //rename Daysw8hrAvgOzoneovrNAAQS Days_w_over_8hr_Ozone 
 rename PersonsinPoverty Perc_in_Poverty
 rename Females65withoutHealthI Females_Under_65_no_Health_Ins
 gen Number_of_MDs_Per_10k = MDsNonFedandFedTotalAct/Cnty_Population*10000  
 gen Number_of_ObGyn_Per_10k = MDsNFObGynGeneralTotal/Cnty_Population*10000  
  gen Inpatient_days_in_cnty= TotHospsInpatientDays/Cnty_Population
  gen Outpatient_days_in_cnty = TotHospsOutpatientVisits/Cnty_Population
  drop TotalNumberHospitals TotHospsInpatientDays TotHospsOutpatientVisits STGFTETotalFacilPrsnl  ///
TotalNursingHomeLicensedBeds STGHospwObstetricCare MDsNonFedandFedTotalAct MDsNFObGynGeneralTotal ///
MDsNonFedandFedTotalAct MDsNFObGynGeneralTotal TotalHospitalBeds PopulationDensityperSquareMi MedianHomeValue

ds StCnty, not  //descibe all variables except StCnty
foreach v of varlist `r(varlist)' {
 replace `v' = round(`v',0.1)		
 recast float `v', force
 format %6.1g `v'
     }
   
 save cnty_data_AHRF.dta, replace


///////////////////////////////////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////
 
///////////////////////////////////////////////////////////////////////////////////////////////////////////
                         //merge county and individual level together	
cd "\Data_Preparation"
use Linked16.dta,clear
merge m:1 StCnty using cnty_data_CHR.dta 
drop if missing(preterm) | missing(state)
drop _merge
order preterm combgest StCnty mrstatefips, first

drop state County CountyCode 

merge m:1 StCnty using cnty_data_AHRF.dta 
drop if missing(preterm)
drop _merge PopulationestimateValue Cnty_Population


save Aggregated_2016.dta, replace
///////////////////////////////////////////////////////////////////////////////////////////////////////////
						//Do NOT FORGET to UPDATE R file named "Data_Preparation"
						
						
